plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
}
apply plugin: 'kotlin-android'

android {
    /* App signing key information */
    signingConfigs {
        aidenkoog {
            storeFile file('../keystore/android_aidenkoog_appkey.jks')
            storePassword "aidenkoog0000"
            keyAlias "android_aidenkoog"
            keyPassword "aidenkoog0000"
        }
    }

    compileSdk 33

    defaultConfig {
        applicationId "io.github.aidenkoog.android"
        minSdk 26
        targetSdk 33
        /*
         * -----------------------------------------------------------------------------------------
         * major.minor.hotfix description
         * 1) major (feature) : feature adding
         * 2) minor (major) : critical modification
         * 3) hotfix
         * -----------------------------------------------------------------------------------------
         */
        versionCode 100
        versionName "1.0.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    /* ---------------------------------------------------------------------------------------------
     * Build type
     * 1) release
     * 2) debug
     * 3) stage
     * - Just change the value corresponding to URL_DOMAIN_SSL key for the REST API domain URL change
     * --------------------------------------------------------------------------------------------*/
    buildTypes {
        release {
            debuggable true
            applicationIdSuffix ""
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.aidenkoog
            buildConfigField("String", "URL_DOMAIN_SSL", "\"https://io.github.aidenkoog\"")
            buildConfigField("String", "MQTT_URL", "\"\"")
            manifestPlaceholders = [appLabel: "Android Architecture"]
        }
        debug {
            applicationIdSuffix ".dev"
            signingConfig signingConfigs.aidenkoog
            buildConfigField("String", "URL_DOMAIN_SSL", "\"https://io.github.aidenkoog\"")
            buildConfigField("String", "MQTT_URL", "\"\"")
            manifestPlaceholders = [appLabel: "Android Architecture DEV"]
        }
        stage {
            debuggable true
            applicationIdSuffix ".qa"
            signingConfig signingConfigs.aidenkoog
            buildConfigField("String", "URL_DOMAIN_SSL", "\"https://io.github.aidenkoog\"")
            buildConfigField("String", "MQTT_URL", "\"\"")
            manifestPlaceholders = [appLabel: "Android Architecture STAGE"]
        }

    }

    // Enable data binding option
    android.buildFeatures.dataBinding = true

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    /*
     * This data is combined with the build type data.
     * As a result, you can see the build variant below.
     * 1) krDebug, 2) krStage, 3) krRelease
     */
    flavorDimensions "destination"
    productFlavors {
        kr {
            dimension "destination"
        }
    }

    // APK file's name
    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def project = "AndroidArchitecture"
            def SEP = "_"
            def buildType = variant.buildType.name
            def versionName = variant.versionName
            def versionCode = variant.versionCode
            def date = new Date();
            def formattedDate = date.format('yyyyMMdd_HHmm')
            def newApkName = project + SEP + buildType.toUpperCase() + SEP + versionName + SEP + versionCode + SEP + formattedDate + ".apk"
            outputFileName = new File(newApkName)
        }
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
}

dependencies {
    implementation fileTree(include: ['*.aar'], dir: 'libs')
    implementation 'androidx.core:core-ktx:1.8.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation 'com.google.android.gms:play-services-recaptcha:17.0.1'

    //test
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    //coroutine
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.5"

    //rxjava
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.17'
    implementation "com.squareup.retrofit2:adapter-rxjava2:2.8.0"

    // activity, fragment
    implementation "androidx.activity:activity-ktx:1.5.1"
    implementation "androidx.fragment:fragment-ktx:1.5.1"

    implementation 'com.google.code.gson:gson:2.8.6'

    //RxBinding
    implementation 'com.jakewharton.rxbinding4:rxbinding:4.0.0'

    //retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'

    //okhttp
    implementation 'com.squareup.okhttp3:okhttp:5.0.0-alpha.2'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.8.1'

    implementation "androidx.core:core-ktx:1.8.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    //Logger
    implementation 'com.orhanobut:logger:2.2.0'

    //Navigation UI
    implementation 'androidx.navigation:navigation-fragment-ktx:2.5.1'
    implementation 'androidx.navigation:navigation-ui-ktx:2.5.1'

    //MQTT
    implementation "com.amazonaws:aws-android-sdk-core:2.27.0"
    implementation "com.amazonaws:aws-android-sdk-cognitoidentityprovider:2.27.0"
    implementation("com.amazonaws:aws-android-sdk-iot:2.27.0") {
        exclude group: 'org.eclipse.paho', module: 'org.eclipse.paho.client.mqttv3'
    }
    implementation "org.eclipse.paho:org.eclipse.paho.client.mqttv3:1.2.5"
    implementation "com.amazonaws:aws-android-sdk-mobile-client:2.16.12"

    //Lottie
    implementation 'com.airbnb.android:lottie:3.0.7'
}